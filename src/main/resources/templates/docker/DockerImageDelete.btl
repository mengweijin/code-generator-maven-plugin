@echo off

echo Deleting docker container and image

set REPOSITORY=${ARTIFACT_ID}
set TAG=${VERSION}

for /f "skip=1 tokens=1,2 delims= " %%i in ('docker ps -a') do (
    if "%REPOSITORY%:%TAG%"=="%%j" (
        echo delete docker container %%i %%j
        docker rm -f %%i
    )
)

rem skip=1 skip the first row
rem tokens=1,2,3 Get only the 1,2,3 entries of the delims delimiter, and the three corresponding variables are also required in do() to receive, for example: echo %%i %%j %%k
rem delims=  A string separated by a space on each line in the list
rem %%i in ('docker images') %%i is used to receive the values returned by the 'docker images' command and delimit them according to delims
rem do() The code in do() is executed once for each line in the list
for /f "skip=1 tokens=1,2,3 delims= " %%i in ('docker images') do (
    if "%REPOSITORY%"=="%%i" (
        if "%TAG%"=="%%j" (
            echo delete docker image %%i %%j %%k
            docker rmi -f %%k
        )
    )
)
